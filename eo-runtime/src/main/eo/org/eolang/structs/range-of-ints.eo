+alias org.eolang.structs.range
+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang.structs
+version 0.0.0
+spdx SPDX-FileCopyrightText: Copyright (c) 2016-2025 Objectionary.com
+spdx SPDX-License-Identifier: MIT
+unlint redundant-object:27

# Range of integers from `start` to `end` (soft border) with step = 1.
# Here `start` and `end` must be `int`s. If they're not - an error will
# be returned.
[start end] > range-of-ints
  if. > @
    and.
      or.
        0.eq start
        1.eq (start.div start)
      or.
        0.eq end
        1.eq (end.div end)
    range
      [] >>
        build ^.start > @
        [num] > build
          num > @
          ^.build (1.plus @) > next
      end
    error "Some of the arguments are not integers"

  # Tests that simple range of integers from one to ten works correctly.
  [] +> tests-simple-range-of-ints-from-one-to-ten
    eq. > @
      range-of-ints 1 10
      * 1 2 3 4 5 6 7 8 9

  # Tests that range of integers from ten to ten is empty.
  [] +> tests-range-of-ints-from-ten-to-ten-is-empty
    is-empty. > @
      range-of-ints 10 10

  # Tests that range of descending integers is empty.
  [] +> tests-range-of-descending-ints-is-empty
    is-empty. > @
      range-of-ints 10 1

  # Tests that range of negative integers works correctly.
  [] +> tests-range-of-negative-ints-works-as-well
    eq. > @
      range-of-ints -5 0
      * -5 -4 -3 -2 -1

  # Tests that range throws error with non-integer start.
  [] +> throws-on-range-of-ints-with-non-int-start
    range-of-ints 1.2 3 > @

  # Tests that range throws error with non-integer end.
  [] +> throws-on-range-of-ints-with-not-int-end
    range-of-ints 1 2.5 > @

  # Tests that range throws error when neither start nor end are integers.
  [] +> throws-on-range-of-not-ints
    range-of-ints 1.5 5.2 > @
